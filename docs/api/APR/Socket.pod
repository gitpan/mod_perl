=head1 NAME

APR::Socket - Perl API for APR sockets




=head1 Synopsis

  use APR::Socket ();
  
  ### set the socket to the blocking mode if it isn't already
  ### and read in the loop and echo it back
  use APR::Const -compile => qw(SO_NONBLOCK);
  if ($sock->opt_get(APR::SO_NONBLOCK)) {
      $sock->opt_set(APR::SO_NONBLOCK => 0);
  }
  # read from/write to the socket (w/o handling possible failures)
  my $wanted = 1024;
  while (1) {
      # read from the socket
      my $buff = $sock->recv($wanted);
      my $rlen = length $buff;
      last if $rlen == 0; # EOF
  
      # write to the socket
      my $wlen = $sock->send($buff);
      last if $wlen != $rlen; # shouldn't happen
  }

  ### get/set IO timeout and try to read some data
  use APR::Const -compile => qw(TIMEUP);
  # timeout is in usecs!
  my $timeout = $sock->timeout_get();
  if ($timeout < 10_000_000) {
      $sock->timeout_set(20_000_000); # 20 secs
  }
  # now read, while handling timeouts
  my $wanted = 1024;
  my $buff = eval { $sock->recv($wanted) };
  if ($@ && ref $@ && $@ == APR::TIMEUP) {
      # timeout, do something, e.g.
      warn "timed out, will try again later";
  }
  else {
      my $rlen = length $buff;
      warn "asked for $wanted bytes, read $rlen bytes\n";
  }


=head1 Description

C<APR::Socket> provides the Perl interface to APR sockets.




=head1 API

C<APR::Socket> provides the following methods:










=head2 C<opt_get>

Query socket options for the specified socket

  $val = $sock->opt_get($opt);

=over 4

=item obj: C<$sock>
( C<L<APR::Socket object|docs::2.0::api::APR::Socket>> )

the socket object to query

=item arg1: C<$opt>
( C<L<APR::Const constant|docs::2.0::api::APR::Const/C__socket_>> )

the socket option we would like to configure.  Here are the
L<available socket options|docs::2.0::api::APR::Const/C__socket_>.

=item ret: C<$val> ( integer )

the currently set value for L<the socket
option|docs::2.0::api::APR::Const/C__socket_> you've queried for

=item excpt: C<L<APR::Error|docs::2.0::api::APR::Error>>

=item since: 1.99_14

=back

Examples can be found in L<the socket options constants
section|docs::2.0::api::APR::Const/C__socket_>. For example L<setting 
the IO to the blocking
mode|docs::2.0::api::APR::Const/C_APR__SO_NONBLOCK_>.




=head2 C<opt_set>

Setup socket options for the specified socket

  $sock->opt_set($opt, $val);

=over 4

=item obj: C<$sock>
( C<L<APR::Socket|docs::2.0::api::APR::Socket>> object )

the socket object to set up.

=item arg1: C<$opt>
( C<L<APR::Const|docs::2.0::api::APR::Const/C__socket_>> constant )

the socket option we would like to configure.  Here are the
L<available socket options|docs::2.0::api::APR::Const/C__socket_>.

=item arg2: C<$val> ( integer )

value for the option. Refer to the L<socket
options|docs::2.0::api::APR::Const/C__socket_> section to learn about
the expected values.

=item ret: no return value

=item excpt: C<L<APR::Error|docs::2.0::api::APR::Error>>

=item since: 1.99_14

=back

Examples can be found in L<the socket options constants
section|docs::2.0::api::APR::Const/C__socket_>. For example L<setting 
the IO to the blocking
mode|docs::2.0::api::APR::Const/C_APR__SO_NONBLOCK_>.





=head2 C<recv>

Read incoming data from the socket

  my $buff = $sock->recv($wanted);

=over 4

=item obj: C<$sock>
( C<L<APR::SockAddr|docs::2.0::api::APR::SockAddr>> object )

The socket to read from

=item arg1: C<$wanted> ( int )

How many bytes to attempt to read.

=item ret: C<$buf> ( string )

C<$buf> gets populated with the string that is read. It will contain
an empty string if there is nothing to read.

=item excpt: C<L<APR::Error|docs::2.0::api::APR::Error>>

If timeout was set via C<timeout_set|/C_timeout_set_>, you may need to
catch C<L<APR::TIMEUP|docs::2.0::api::APR::Const/C_APR__TIMEUP_>>
exceptions. For example:

  use APR::Const -compile => qw(TIMEUP);
  my $buff = eval { $sock->recv($wanted) };
  if ($@ && $@ == APR::TIMEUP) {
      # timeout, do something, e.g.
  }

=item since: 1.99_14

=back

Examples:

  use APR::Socket ();
  my $wanted = 1024;
  while (1) {
      # read from the socket
      my $buff = $sock->recv($wanted);
      my $rlen = length $buff;
      last if $rlen == 0; # EOF
  
      # write to the socket
      my $wlen = $sock->send($buff);
      last if $wlen != $rlen; # shouldn't happen
  }











=head2 C<send>

Write data to the socket

  $wlen = $sock->send($buf, $opt_len);

=over 4

=item obj: C<$sock> ( C<L<APR::Socket|docs::2.0::api::APR::Socket>> )

The socket to write to

=item arg1: C<$buf> ( scalar )

The data to send

=item opt arg2: C<$opt_len> ( int )

There is no need to pass this argument, unless you want to send less
data than contained in C<$buf>.

=item ret: C<$wlen> ( integer )

How many bytes were sent

=item since: 1.99_14

=back

For examples see the C<L<recv|/C_recv_>> item.











=head2 C<timeout_get>

Get socket timeout settings

  $usecs = $sock->timeout_get();

=over 4

=item obj: C<$sock> ( C<L<APR::Socket|docs::2.0::api::APR::Socket>> )

The socket to set up.

=item ret: C<$usecs> ( number)

Currently set timeout in microseconds (and also the blocking IO
behavior). See (C<L<APR::timeout_set|/C__timeout_set_>>) for possible
values and their meaning.

=item excpt: C<L<APR::Error|docs::2.0::api::APR::Error>>

=item since: 1.99_14

=back


















=head2 C<timeout_set>

Setup socket timeout.

  $sock->timeout_set($usecs);

=over 4

=item obj: C<$sock> ( C<L<APR::Socket|docs::2.0::api::APR::Socket>> )

The socket to set up.

=item arg1: C<$usecs> ( number )

Value for the timeout in microseconds and also the blocking IO
behavior.

The possible values are:

=over

=item t E<gt> 0

C<L<send()|/C__send_>> and C<L<recv()|/C__recv_)>> throw
(C<L<APR::TIMEUP|docs::2.0::api::APR::Const/C__APR__TIMEUP_>>
exception) if specified time elapses with no data sent or received.

Notice that the positive value is in micro seconds. So if you want to
set the timeout for 5 seconds, the value should be: 5_000_000.

This mode sets the socket into a non-blocking IO mode.

=item t == 0

C<L<send()|/C__send_>> and C<L<recv()|/C__recv_)>> calls never block.

=item t E<lt> 0

C<L<send()|/C__send_>> and C<L<recv()|/C__recv_)>> calls block.

Usually just -1 is used for this case, but any negative value will do.

This mode sets the socket into a blocking IO mode.

=item ret: no return value

=back

=item excpt: C<L<APR::Error|docs::2.0::api::APR::Error>>

=item since: 1.99_14

=back
















=head1 Unsupported API

C<APR::Socket> also provides auto-generated perl interface for a few
other methods which aren't tested at the moment and therefore their
API is a subject to change. These methods will be finalized later as a
need arises. If you want to rely on any of the following methods
please contact the L<the mod_perl development mailing list|maillist::dev>
so we can help eachother take the steps necessary to shift the method
to an officially supported API.





=head2 C<bind>

META: Autogenerated - needs to be reviewed/completed

Bind the socket to its associated port

  $ret = $sock->bind($sa);

=over 4

=item obj: C<$sock> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The socket to bind

=item arg1: C<$sa> (C<L<APR::SockAddr|docs::2.0::api::APR::SockAddr>>)

The socket address to bind to

=item ret: C<$ret> (integer)

=item since: subject to change

=back

This may be where we will find out if there is any other process
using the selected port.






=head2 C<close>

META: Autogenerated - needs to be reviewed/completed

Close a socket.

  $ret = $thesocket->close();

=over 4

=item obj: C<$thesocket> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The socket to close

=item ret: C<$ret> (integer)


=item since: subject to change


=back





=head2 C<connect>

META: Autogenerated - needs to be reviewed/completed

Issue a connection request to a socket either on the same machine
or a different one.

  $ret = $sock->connect($sa);

=over 4

=item obj: C<$sock> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The socket we wish to use for our side of the connection 

=item arg1: C<$sa> (C<L<APR::SockAddr|docs::2.0::api::APR::SockAdrr>>)

The address of the machine we wish to connect to.  If NULL,
APR assumes that the sockaddr_in in the apr_socket is
completely filled out.

=item ret: C<$ret> (integer)

=item since: subject to change


=back





=head2 C<listen>

META: Autogenerated - needs to be reviewed/completed

Listen to a bound socket for connections.

  $ret = $sock->listen($backlog);

=over 4

=item obj: C<$sock> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The socket to listen on 

=item arg1: C<$backlog> (integer)

The number of outstanding connections allowed in the sockets
listen queue.  If this value is less than zero, the listen
queue size is set to zero.

=item ret: C<$ret> (integer)

=item since: subject to change


=back










=head2 C<recvfrom>

META: Autogenerated - needs to be reviewed/completed



  $ret = $from->recvfrom($sock, $flags, $buf, $len);

=over 4

=item obj: C<$from> (C<L<APR::SockAddr|docs::2.0::api::APR::SockAddr>>)

The apr_sockaddr_t to fill in the recipient info

=item arg1: C<$sock> (C<L<APR::SockAddr|docs::2.0::api::APR::SockAddr>>)

The socket to use

=item arg2: C<$flags> (integer)

The flags to use

=item arg3: C<$buf> (integer)

The buffer to use

=item arg4: C<$len> (string)

The length of the available buffer

=item ret: C<$ret> (integer)

=item since: subject to change


=back





=head2 C<sendto>

META: Autogenerated - needs to be reviewed/completed



  $ret = $sock->sendto($where, $flags, $buf, $len);

=over 4

=item obj: C<$sock> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The socket to send from

=item arg1: C<$where> (C<L<APR::Socket|docs::2.0::api::APR::Socket>>)

The apr_sockaddr_t describing where to send the data

=item arg2: C<$flags> (integer)

The flags to use

=item arg3: C<$buf> (scalar)

The data to send

=item arg4: C<$len> (string)

The length of the data to send

=item ret: C<$ret> (integer)

=item since: subject to change

=back








=head1 See Also

L<mod_perl 2.0 documentation|docs::2.0::index>.




=head1 Copyright

mod_perl 2.0 and its core modules are copyrighted under
The Apache Software License, Version 1.1.




=head1 Authors

L<The mod_perl development team and numerous
contributors|about::contributors::people>.

=cut

